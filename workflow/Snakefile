configfile: "config/config.yaml"
configfile: "config/samples.yaml" 


rule test:
    input:
        expand(
            "results/statistics/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{grid}.stats.json",
            model=["ridge", "majority-classifier"],
            dataset=["test"],
            features=["x"],
            targets=["y"],
            matching=["y"],
            stratify=["0"],
            grid=["default"],
        ),


rule split:
    input:
        features="resources/{dataset}/features/{features}.csv",
        targets="resources/{dataset}/targets/{targets}.csv",
        matching="resources/{dataset}/matching/{matching}.csv",
    output:
        split="results/splits/{dataset}/{features}_{targets}_{matching}_{stratify}_{samplesize}_{seed}.json",
    script:
        "scripts/generate_splits.py"


rule fit:
    input:
        features="resources/{dataset}/features/{features}.csv",
        targets="resources/{dataset}/targets/{targets}.csv",
        split="results/splits/{dataset}/{features}_{targets}_{matching}_{stratify}_{samplesize}_{seed}.json",
        grid="config/grids/{grid}.yaml",
    output:
        scores="results/fits/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{samplesize}_{seed}_{grid}.csv",
    script:
        "scripts/fit.py"


rule aggregate:
    input:
        scores=expand(
            "results/fits/{{model}}/{{dataset}}/{{features}}_{{targets}}_{{matching}}_{{stratify}}_{samplesize}_{seed}_{{grid}}.csv",
            seed=config["seeds"],
            samplesize=config["sample_sizes"],
        ),
    output:
        scores="results/scores/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{grid}.csv",
    script:
        "scripts/aggregate.py"



rule extrapolate:
    input:
        scores="results/scores/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{grid}.csv",
    output:
        stats="results/statistics/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{grid}.stats.json",
        bootstraps="results/statistics/{model}/{dataset}/{features}_{targets}_{matching}_{stratify}_{grid}.boostrap.json",
    script:
        "scripts/extrapolate.py"
